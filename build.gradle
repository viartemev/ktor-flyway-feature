buildscript {
    ext {
        ktor_version = '1.6.0'
        logback_version = '1.2.3'
    }
    repositories {
        jcenter()
    }
}

plugins {
    id 'java'
    id 'maven'
    id 'maven-publish'
    id "com.jfrog.bintray" version "1.8.5"
    id 'com.adarshr.test-logger' version '3.0.0'
    id 'org.jetbrains.kotlin.jvm' version '1.5.10'
}

group 'com.viartemev'
version '1.2.2'

sourceCompatibility = 1.8

repositories {
    mavenCentral()
    jcenter()
}

dependencies {
    implementation "org.flywaydb:flyway-core:7.9.1"
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8"
    runtime "ch.qos.logback:logback-classic:$logback_version"
    implementation "io.ktor:ktor-server-core:$ktor_version"
    testCompile "io.ktor:ktor-server-test-host:$ktor_version"
    testCompile "org.testcontainers:postgresql:1.15.3"
    testCompile "org.testcontainers:testcontainers:1.15.3"
    testCompile "org.testcontainers:junit-jupiter:1.15.3"
    testCompile "com.zaxxer:HikariCP:4.0.3"
    testCompile "org.postgresql:postgresql:42.2.20"
    testCompile "org.junit.jupiter:junit-jupiter-api:5.7.2"
    testCompile "org.junit.jupiter:junit-jupiter-params:5.7.2"
    testRuntime "org.junit.jupiter:junit-jupiter-engine:5.7.2"
}

compileKotlin {
    kotlinOptions.jvmTarget = "1.8"
}
compileTestKotlin {
    kotlinOptions.jvmTarget = "1.8"
}

jar {
    archiveBaseName = "ktor-flyway-feature"
    archiveVersion = "1.2.2"
}

test {
    useJUnitPlatform()
}

task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

artifacts {
    archives jar
    archives sourcesJar
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            artifact(sourcesJar) {
                classifier = 'sources'
            }
            pom {
                groupId = 'com.viartemev'
                artifactId = 'ktor-flyway-feature'

                from components.kotlin

                name = 'Ktor Flyway feature'
                description = 'Ktor flyway feature'
                url = 'https://github.com/viartemev/ktor-flyway-feature'
                licenses {
                    license {
                        name = 'MIT'
                        url = 'https://raw.githubusercontent.com/viartemev/ktor-flyway-feature/master/LICENSE'
                    }
                }
                developers {
                    developer {
                        id = 'viartemev'
                        name = 'Vyacheslav Artemyev'
                        email = 'viartemev@gmail.com'
                    }
                }
                scm {
                    connection = 'scm:git:git://github.com/viartemev/ktor-flyway-feature.git'
                    developerConnection = 'scm:git:ssh://github.com/viartemev/ktor-flyway-feature.git'
                    url = 'https://github.com/viartemev/ktor-flyway-feature'
                }
            }
        }
    }
}

bintray {
    user = System.getenv('BINTRAY_USER')
    key = System.getenv('BINTRAY_KEY')
    publications = ['mavenJava']
    pkg {
        userOrg = 'viartemev'
        repo = 'Maven'
        name = 'ktor-flyway-feature'
        licenses = ['MIT']
        desc = 'Ktor flyway feature'
        websiteUrl = 'https://github.com/viartemev/ktor-flyway-feature'
        issueTrackerUrl = 'https://github.com/viartemev/ktor-flyway-feature/issues'
        vcsUrl = 'https://github.com/viartemev/ktor-flyway-feature.git'
        publicDownloadNumbers = true
    }
}
