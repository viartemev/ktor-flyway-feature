buildscript {
    ext {
        ktor_version = '1.6.3'
        logback_version = '1.2.6'
    }
}

plugins {
    id 'java'
    id 'signing'
    id 'maven-publish'
    id 'com.adarshr.test-logger' version '3.0.0'
    id 'org.jetbrains.kotlin.jvm' version '1.5.31'
}

group = 'com.viartemev'
version = '1.3.0'
archivesBaseName = "ktor-flyway-feature"

sourceCompatibility = 1.8

repositories {
    mavenCentral()
}

dependencies {
    implementation "org.flywaydb:flyway-core:7.15.0"
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8"
    runtimeOnly "ch.qos.logback:logback-classic:$logback_version"
    implementation "io.ktor:ktor-server-core:$ktor_version"
    testImplementation "io.ktor:ktor-server-test-host:$ktor_version"
    testImplementation "org.testcontainers:postgresql:1.16.0"
    testImplementation "org.testcontainers:testcontainers:1.16.0"
    testImplementation "org.testcontainers:junit-jupiter:1.16.0"
    testImplementation "com.zaxxer:HikariCP:5.0.0"
    testImplementation "org.postgresql:postgresql:42.2.24"
    testImplementation "org.junit.jupiter:junit-jupiter-api:5.8.1"
    testImplementation "org.junit.jupiter:junit-jupiter-params:5.8.1"
    testRuntimeOnly "org.junit.jupiter:junit-jupiter-engine:5.8.1"
}

compileKotlin {
    kotlinOptions.jvmTarget = "1.8"
}
compileTestKotlin {
    kotlinOptions.jvmTarget = "1.8"
}

jar {
    archiveBaseName = "ktor-flyway-feature"
    archiveVersion = "1.3.0"
}

test {
    useJUnitPlatform()
}

task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

task javadocJar(type: Jar) {
    classifier = 'javadoc'
    from javadoc
}

artifacts {
    archives jar
    archives sourcesJar
    archives javadocJar
}

signing {
    useGpgCmd()
    sign configurations.archives
}

uploadArchives {
    repositories {
        mavenDeployer {
            beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

            repository(url: "https://s01.oss.sonatype.org/service/local/staging/deploy/maven2/") {
                authentication(userName: ossrhUsername, password: ossrhPassword)
            }

            snapshotRepository(url: "https://s01.oss.sonatype.org/content/repositories/snapshots/") {
                authentication(userName: ossrhUsername, password: ossrhPassword)
            }

            pom.project {
                name 'Ktor Flyway feature'
                packaging 'jar'
                // optionally artifactId can be defined here
                description 'Ktor flyway feature'
                url 'https://github.com/viartemev/ktor-flyway-feature'

                scm {
                    connection 'scm:git:git://github.com/viartemev/ktor-flyway-feature.git'
                    developerConnection 'scm:git:ssh://github.com/viartemev/ktor-flyway-feature.git'
                    url 'https://github.com/viartemev/ktor-flyway-feature'
                }

                licenses {
                    license {
                        name 'MIT'
                        url 'https://raw.githubusercontent.com/viartemev/ktor-flyway-feature/master/LICENSE'
                    }
                }

                developers {
                    developer {
                        id 'viartemev'
                        name 'Vyacheslav Artemyev'
                        email 'viartemev@gmail.com'
                    }
                }
            }
        }
    }
}